apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "io.fabric"
apply plugin: "com.google.firebase.firebase-perf"

def applicationName = "OrientationFaker"
def versionMajor = 3
def versionMinor = 1
def versionPatch = 1

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "net.mm2d.android.orientationfaker"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
        archivesBaseName = "${applicationName}-${versionName}"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
            versionNameSuffix = "d"
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.all {
            outputFileName = "${applicationName}-${versionName}-google-play.apk"
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.preference:preference:1.1.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "net.mm2d:log:0.9.1"
    implementation "net.mm2d:log-android:0.9.1"
    implementation "net.mm2d:color-chooser:0.1.3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
    implementation "io.reactivex.rxjava2:rxjava:2.2.16"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'

    implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"
    implementation "com.google.firebase:firebase-core:17.2.1"
    implementation "com.google.firebase:firebase-ads:18.3.0"
    implementation "com.google.firebase:firebase-perf:19.0.4"
    implementation "com.google.android.ads.consent:consent-library:1.0.8"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
